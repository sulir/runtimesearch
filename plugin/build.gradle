plugins {
    id 'org.jetbrains.intellij' version '0.7.2'
    id 'org.jetbrains.changelog' version '1.1.1'
}

intellij {
    version '2020.3.3'
    plugins = ['java']
    updateSinceUntilBuild = false
    buildSearchableOptions.enabled = false
}

patchPluginXml {
    changeNotes {
        changelog.getAll().collect {
            '<b>' + it.getKey() + '</b>' + it.getValue().toHTML()
        }.join()
    }
}

changelog {
    path = "${project.rootDir}/CHANGELOG.md"
    headerParserRegex = /(\d+)\.(\d+)/
}

dependencies {
    runtimeOnly(project(':runtimesearch-agent')) {
        transitive = false
    }
}

buildPlugin {
    destinationDirectory = project.rootProject.file('dist')
    archiveFileName = "${project.name}.zip"
}

runIde {
    jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED'
}

clean {
    delete buildPlugin.archiveFile
}