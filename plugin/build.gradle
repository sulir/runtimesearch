plugins {
    id 'org.jetbrains.intellij' version '1.2.1'
    id 'org.jetbrains.changelog' version '1.3.1'
}

dependencies {
    implementation project(':runtimesearch-shared')
    runtimeOnly(project(':runtimesearch-agent')) {
        transitive = false
    }
}

def intellijVersions = ['2020.1.4', '2020.2.4', '2020.3.3', '2021.1.3', '2021.2.3']

intellij {
    version = intellijVersions.last()
    plugins = ['java']
    updateSinceUntilBuild = false
    buildSearchableOptions.enabled = false
}

changelog {
    version = rootProject.version
    path = "${project.rootDir}/CHANGELOG.md"
    headerParserRegex = /(\d+\.\d+)/
    unreleasedTerm = 'Unreleased'
}

patchPluginXml {
    pluginDescription = {
        def lines = rootProject.file('README.md').readLines()
        def start = lines.indexOf('<!--plugin-desc-->')
        def end = lines.indexOf('<!--/plugin-desc-->')
        def description = lines.subList(start + 1, end).join('\n')
        String readme = org.jetbrains.changelog.ExtensionsKt.markdownToHTML(description)

        '<p>Searches the given text in all string expressions of a debugged Java SE program.<p>\n\n' + readme
    }()

    changeNotes = {
        changelog.getAll().collect {
            '<b>' + it.getKey() + '</b>' + it.getValue().toHTML()
        }.join()
    }()
}

buildPlugin {
    dependsOn ':runtimesearch-agent:jar'
    destinationDirectory = project.rootProject.file('dist')
}

runPluginVerifier {
    ideVersions = intellijVersions
}

publishPlugin {
    token = System.getenv('PUBLISH_TOKEN')
    channels = [version.contains('snapshot') ? 'snapshot' : 'default']
}

runIde {
    dependsOn ':runtimesearch-agent:jar'
    jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED'
}

clean {
    delete buildPlugin.archiveFile
}