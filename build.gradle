plugins {
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
    id 'org.jetbrains.changelog' version '1.1.1'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.15'
}

jgitver {
    useDirty = true
    policy {
        pattern = '(.*)'
        transformations = ['IGNORE']
    }
}

changelog {
    headerParserRegex = /(\d+)\.(\d+)/
    unreleasedTerm = 'Unreleased'
}

dependencyUpdates {
    revision = 'release'
    rejectVersionIf {
        it.candidate.version.contains('-')
    }
}

allprojects {
    project.afterEvaluate {
        def release = (version =~ /(\d+.\d+).0/)
        version = release.matches() ? release.group(1) : 'snapshot'
        changelog.version = version
    }

    apply plugin: 'se.patrikerdes.use-latest-versions'
    apply plugin: 'com.github.ben-manes.versions'
}

subprojects {
    group = 'com.github.sulir.runtimesearch'

    apply plugin: 'java'
    sourceCompatibility = 1.8

    apply plugin: 'idea'
    idea {
        module {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
    }

    test {
        useJUnitPlatform()
    }
}