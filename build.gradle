plugins {
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
    id 'com.github.ben-manes.versions' version '0.41.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
}

jgitver {
    useDirty = true
    policy {
        pattern = '(.*)'
        transformations = ['IGNORE']
    }
}

dependencyUpdates {
    revision = 'release'
    rejectVersionIf {
        it.candidate.version.contains('-')
    }
}

allprojects {
    group = 'com.github.sulir.runtimesearch'

    project.afterEvaluate {
        def release = (version =~ /(\d+.\d+).0/)
        version = release.matches() ? release.group(1) : 'snapshot'
    }

    apply plugin: 'se.patrikerdes.use-latest-versions'
    apply plugin: 'com.github.ben-manes.versions'
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8

    apply plugin: 'idea'
    idea {
        module {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    test {
        useJUnitPlatform()
    }

    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        from (rootProject.file('LICENSE.txt')) {
            into 'META-INF/'
        }
    }
}