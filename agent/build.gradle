plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    implementation 'org.ow2.asm:asm:9.7.1'
    implementation 'org.ow2.asm:asm-tree:9.7.1'
    implementation 'org.ow2.asm:asm-analysis:9.7.1'
    implementation project(':runtimesearch-shared')
}

def agentFileName = project.name + ".jar"

shadowJar {
    destinationDirectory = project.rootProject.file('dist')
    archiveFileName = agentFileName
    relocate 'org.objectweb.asm', 'com.github.sulir.runtimesearch.renamed.asm'

    manifest {
        attributes(
                'Premain-Class': 'com.github.sulir.runtimesearch.agent.SearchAgent',
                'Boot-Class-Path': agentFileName
        )
    }
}

jar {
    destinationDirectory = shadowJar.destinationDirectory
    archiveFileName = shadowJar.archiveFileName
    enabled = false
    dependsOn shadowJar
}

clean {
    delete jar.archiveFile
}